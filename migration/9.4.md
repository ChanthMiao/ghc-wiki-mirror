[[_TOC_]]

# GHC 9.4.x Migration Guide

This guide summarises the changes you may need to make to your code to migrate from GHC 9.2 to GHC 9.4. This guide complements the GHC 9.4.x release notes which should be consulted as well.

---

## GHC API changes

* The GHC API now supports multiple home units. The crux of this change is that instead of the driver session maintaining a HomePackageTable, which contains the interface files for the single home unit, there is now a HomeUnitGraph, which contains a set of HomePackageTables indexed by their unit id. By using this new interface it's easier to write GHC API programs which work nicely with multiple packages as whole projects can be loaded and typechecked in one go. Existing GHC API applications should not have to be rewritten to account for this change as
there are still a number of backwards-compatibility functions which assume that there is only one home package.

## Compiler changes

### Ambiguous types containing a TypeError

GHC rejects certain ambiguous types when they contain an insoluble constraint, even with `-XAllowAmbiguousTypes`. For example:

```haskell
{-# LANGUAGE AllowAmbiguousTypes #-}

class C (a :: Type)
instance (Maybe w ~ Bool) => C a
```

This program is rejected because `w` is ambiguous and `Maybe w ~ Bool` is an insoluble constraint. This behaviour isn't new, but it now extends to `TypeError` constraints too. For example, the following program used to be accepted, but it is now rejected:

```haskell
{-# LANGUAGE AllowAmbiguousTypes #-}

class C (a :: Type)
instance TypeError (ShowType w) => C a
```

In practice, the ambiguity might be hidden in a kind (especially now that `-XPolyKinds` is enabled by default, with `-XGHC2021`). For example:

```haskell
class C (a :: Type)
instance TypeError (ShowType Any) => C a
```

is ambiguous because the kind of `Any` is not specified. Specifying a kind, e.g.:

```haskell
class C (a :: Type)
instance TypeError (ShowType (Any @Type)) => C a
```

allows the program to be accepted again.

### Changes around hs-boot files

Previous versions of GHC were not very precise about adding dependencies on hs-boot files therefore there were situations where a boot file existed but it wasn't compiled. Now we make sure corresponding boot files are compiled, so you may encounter errors relating to unused boot files. The correct fix is probably to just delete the file.

### `(~)` is now a type operator

GHC now implements [proposal #371](https://github.com/ghc-proposals/ghc-proposals/blob/27b3ca5bf6c447a0a79c9a0c97277d26006fc7ce/proposals/0371-non-magical-eq.md). As such, the use of `(~)` now requires `TypeOperators` instead of `GADTs` or `TypeFamilies`, and it is exported by `Data.Type.Equality` and `Prelude`. For backwards-compatibility reasons, old code using `(~)` that does not meet these criteria will continue to compile, but it may produce warnings:

* If `(~)` is used but `TypeOperators` is not enabled, GHC will produce a `-Wtype-equality-requires-operators` warning. This warning can be fixed by enabling `TypeOperators`.
* If `(~)` is used but is not imported transitively via `Data.Type.Equality` or `Prelude`, GHC will produce a `-Wtype-equality-out-of-scope` warning. This warning can be fixed by either importing one of these modules or, in the case where a custom `Prelude` is being used, re-exporting `(~)` from the custom `Prelude`.

### No generalization over type variables determined by functional dependencies

A small change has been made to the way GHC infers types for definitions with no type signature: GHC will no longer generalize a function over a type variable determined by a functional dependency. For example:

```haskell
class C a b | a -> b where
  op :: a -> b -> ()

f x = op True x
```

Previously, GHC would infer `f :: forall b. C Bool b => b -> ()`. However, the functional dependency says that only one type could ever be used for `b`: this function is hardly valid "for all" `b` s. GHC 9.4 will reject this program, looking for the (non-existent) instance for `C Bool b`:

```plaintext
error:
    • No instance for (C Bool b0) arising from a use of ‘op’
    • In the expression: op True x
      In an equation for ‘f’: f x = op True x
  |
8 | f x = op True x
  |       ^^
```

If you want to retain the old behavior, add a (backward-compatible) type signature to `f`, explicitly requesting this unusual quantification.

### Removal of derived constraints

When GHC's typechecker tries to solve constraints, it can generate new derived constraints. For example, if we have a class with a functional dependency `C a b | a -> b`, if we have evidence for the two constraints `C a b1`, `C a b2` we can derive the equality constraint `b1 ~ b2`. GHC used to represent these derived constraints in a special form, but now they are normal Wanted constraints.

This can impact many situations in ways that aren't always predictable. Usually, it will involve GHC requiring stronger constraints when functional dependencies, injective type families, simplifiable constraints, or superclasses are involved. The end result is
usually that GHC will require additional constraints to be provided in type signatures.

#### Inference for simplifiable constraints

GHC can now require additional constraints that are derived from simplification of (and
interaction between) constraints.

For example, the following program no longer typechecks in GHC 9.4:

```haskell
type family F x

f :: (F a ~ Maybe a, Show (F a)) => a -> IO ()
f _ = pure ()

g :: (F a ~ Maybe a, Show (F a)) => a -> IO ()
g = go
  where
    go = f
```

The problem is that the `Show (F a)` constraint simplifies to `Show a` (using `F a ~ Maybe a` and the top-level `Show` instance for `Maybe`), so GHC wants to infer a type for `go` which includes the constraint `Show a`, causing the following error:

```plaintext
error:
    • Could not deduce (Show a) arising from a use of ‘f’
      from the context: (F a ~ Maybe a, Show (F a))
      ...
    • In the expression: f
      In an equation for ‘go’: go = f
      ...
```

To fix the error, add an explicit type signature to `go`:

```haskell
g :: (F a ~ Maybe a, Show (F a)) => a -> IO ()
g = go
  where
    go :: (F a ~ Maybe a, Show (F a)) => a -> IO ()
    go = f
```

#### Constraints derived from superclasses

GHC is similarly more restrictive with the following program

```hs
type family F x

class C a b | b -> a

class C (F b) b => D b

f :: (C () b, D b) => Maybe b
f = g

g :: (C a b) => Maybe b
g = Nothing
```

In this situation, when typechecking `f`, GHC will see that we can obtain evidence for `C (F b) b` from `D b` because of the superclass on `D`. Thus we get both `C () b` and `C (F b) b`, and because of the functional dependency on `C`, GHC will require `F () ~ b`. As that constraint wasn't provided in the type signature, the program is rejected. To make this program compile with 9.4 or later, add that as an explicit constraint:

```hs
f :: (C () b, D b, F b ~ ()) => Maybe b
f = g
```

#### GHC rejects ambiguous situations when solving for injective type families

There were previously cases around functional dependencies and injective type families where the result of type inference would depend on the order of constraints, as written in a source file. These cases are fundamentally ambiguous. While GHC previously made an arbitrary decision, it now notices the ambiguity and rejects the program.

This means that some previously accepted programs are now rejected. Here is a concrete example of this:

```haskell
type Plus1 :: Nat -> Nat
type family Plus1 n = r | r -> n

data V (n :: Nat) = V

data VSucc n where
  VSucc :: V n -> VSucc (Plus1 n)

foo :: VSucc n -> VSucc n -> VSucc n
foo (VSucc @n1 _) (VSucc @n2 _) = VSucc V
```

When typechecking `VSucc V`, GHC must figure out what the `n3` in `VSucc @n3 (V @n3)` should be. Note that `n1` and `n2` are both possible choices, which leads to ambiguity. While earlier versions of GHC would just pick one of `n1` or `n2`, GHC 9.4 will notice the ambiguity and reject it:

```plaintext
error:
    • Could not deduce (n2 ~ n1)
        arising from reasoning about an injective type family using constraints:
          ‘Plus1 n2 ~ n’
            arising from a type equality
              VSucc (Plus1 n2) ~ VSucc n at Bug.hs:26:35-41
          ‘Plus1 n1 ~ n’
            arising from a pattern with constructor:
                           VSucc :: forall (n :: Nat). V n -> VSucc (Plus1 n),
                         in an equation for ‘foo’ at Bug.hs:26:6-16
      ...
    • In the expression: VSucc V
      In an equation for ‘foo’: foo (VSucc @n1 _) (VSucc @n2 _) = VSucc V
   |
26 | foo (VSucc @n1 _) (VSucc @n2 _) = VSucc V
   |                                   ^^^^^^^
```

To make GHC 9.4 accept this program, tell GHC how you want to instantiate `VSucc`:

```haskell
foo :: VSucc n -> VSucc n -> VSucc n
foo (VSucc @n1 _) (VSucc @n2 _) = VSucc @n1 V -- Or, `VSucc @n2 V`
```

#### Impact on typechecking plugins

The `tcPluginSolve` method of `TcPlugin` now takes one fewer argument (the argument corresponding to Derived constraints has been removed). The `newDerived` function has also been removed; use `newWanted` instead.

### `\cases` is parsed differently when `-XLambdaCase` is enabled

When `-XLambdaCase` is enabled, the sequence `\ cases` (whitespace optional) is now parsed as the herald of a multi-pattern lambda-case expression. Thus GHC will reject a program if you have this extension enabled try to use a lambda expression whose first argument is called `cases`. Doing any of the following will make GHC 9.4 accept the program:

* rename the argument
* change the order such that it's not the first argument of the lambda expression
* disable the extension
* use a named function instead of a lambda expression

### Parser plugins have a different type

The type of `GHC.Plugins.parsedResultAction` was changed from

```haskell
parsedResultAction :: [CommandLineOption] -> ModSummary -> HsParsedModule -> Hsc HsParsedModule
```

to

```haskell
parsedResultAction :: [CommandLineOption] -> ModSummary -> ParsedResult -> Hsc ParsedResult
```

The `ParsedResult` contains both the `HsParsedModule` from before, as well as a `PsMessages` value, which allows you to modify the warning and error messages from the parser, assuming none of them prevented the AST from being built. To retain the old behavior, simply pass the messages through unchanged and only modify the `parsedResultModule` field of the `ParsedResult`.

### Changes to typechecker plugins

Typechecker plugins now support type-family rewriting. This adds one extra field to the `TcPlugin` datatype, `tcPluginRewrite`:

```haskell
data TcPlugin = forall s . TcPlugin
  { tcPluginInit    :: TcPluginM s
  , tcPluginSolve   :: s -> EvBindsVar -> TcPluginSolver
  , tcPluginRewrite :: s -> UniqFM TyCon TcPluginRewriter
  , tcPluginStop    :: s -> TcPluginM ()
  }
```

Authors of type-checking plugins that do not want to use this functionality can set this field to the value `const emptyUFM`, indicating that no type families should be rewritten by the plugin.

In addition, typechecker plugins can solve some constraints while reporting a separate contradiction, at the same time, instead of either/or with `TcPluginOk`/`TcPluginContradiction`. `TcPluginResult` has been renamed to `TcPluginSolveResult` and is now defined as:

```haskell
data TcPluginSolveResult
  = TcPluginSolveResult
      { tcPluginInsolubleCts :: [Ct]
      , tcPluginSolvedCts    :: [(EvTerm, Ct)]
      , tcPluginNewCts       :: [Ct]
      }
```

This datatype bundles pattern synonyms `TcPluginOk` and `TcPluginContradiction` to recover the old behaviour, but authors of typechecking plugins will still need to account for the change in the name.

### Windows uses a Clang-based toolchain

Windows' C/C++ toolchain is now [based on Clang](https://gitlab.haskell.org/ghc/ghc/-/issues/21019) instead of GCC. While most code will be unaffected by this change, there are some situations where packages that use C or C++ code on Windows will need to be adjusted:

#### `gcc_s` no longer needs to be linked against

Some packages previously needed to link against the `gcc_s` library on Windows because the GCC-compiled code used GCC-related intrinsics behind the scenes. This situation does not occur with Clang, so one can simply drop the `gcc_s` dependency on GHC 9.4 or later:

```
if os(windows) && !impl(ghc >= 9.4)
  extra-libraries: gcc_s
```

#### Link against `system-cxx-std-lib` instead of `stdc++`

Previously, libraries that use C++ code would need to link against GCC's `stdc++` library. Clang uses differently named C++ libraries, so this will need to be adjusted. Starting with GHC 9.4, GHC offers a virtual `system-cxx-std-lib` package that can be used to reliably link against the
system's C++ standard library implementation. (This works on all OSes, not just Windows.)

For example, here is how you might write a `.cabal` file that needs to link against C++ code starting with GHC 9.4:

```
if impl(ghc >= 9.4)
  build-depends: system-cxx-std-lib
else
  if os(windows)
    extra-libraries: stdc++
  else
    -- Handle other OSes here...
```

TODO: Make sure this is still accurate after https://gitlab.haskell.org/ghc/ghc/-/issues/21435 and https://gitlab.haskell.org/ghc/ghc/-/issues/21618 are resolved

#### CPP preprocessing differences

Clang's preprocessor only implements a subset of the `-traditional-cpp` flag as compared to GCC's implementation of `-traditional-cpp`. As a result, you may experience preprocessor failures on GHC 9.4 on Windows if you include `#includes` in `{- ... -}` comments. For example:

```hs
{-# LANGUAGE CPP #-}
module Foo where

{-
Here is some C code in a comment:

  #include <stdio.h>
  int main(void) {
    printf("Hello, World!\n");
    return 0;
  }
-}
```

While this will compile on previous versions of GHC, which use GCC's preprocessor, it will not compile with GHC 9.4, as Clang will attempt to expand the commented-out `#include` directive, which confuses the compiler. This issue can be fixed by commenting the `#include` directive with `--` comments instead:

```hs
{-# LANGUAGE CPP #-}
module Foo where

-- Here is some C code in a comment:
-- 
--   #include <stdio.h>
--   int main(void) {
--     printf("Hello, World!\n");
--     return 0;
--   }
```

---

## Library changes

### `base-4.17`

#### Word64/Int64 use Word64#/Int64#

Before 9.4, `Int64/Word64` were wrappers for `Int#/Word#` on 64-bit architectures and for `Int64#/Word64#` on 32-bit architectures. Now the latter are always used (`Int64#/Word64#`). Users of the unboxed values must use `Int64#/Word64#` primops on every architecture.

For example the `Word64` type is now defined as

```
data Word64 = W64# Word64#
```

rather than

```
data Word64 = W64# Word
```

So if you inspecting this representation you should now use the *64 family of primops to
manipulate the internal values rather than the unsuffixed `Word` primops. 

```
-- before
eqWord64 (W64# x) (W64# y) = isTrue# (x `eqWord` y)
-- now
eqWord64 (W64# x) (W64# y) = isTrue# (x `eqWord64` y)
```

#### `ST` is no longer an instance of `MonadFail`

In accordance with [this core libraries proposal](https://github.com/haskell/core-libraries-committee/blob/7ff4d57b842ec87d22077cbf857715d6635f63dc/guides/no-monadfail-st-inst.md), GHC no longer implements `MonadFail` instances for strict or lazy `ST`. See [here](https://github.com/haskell/core-libraries-committee/blob/7ff4d57b842ec87d22077cbf857715d6635f63dc/guides/no-monadfail-st-inst.md#how) for ways to migrate affected code.

### `ghc-prim-0.9`

#### reallyUnsafePtrEquality#

`GHC.Exts.reallyUnsafePtrEquality#` is now levity-polymorphic and heterogeneous, with type:

```haskell
reallyUnsafePtrEquality#
  :: forall {l :: Levity} {k :: Levity}
            (a :: TYPE ('BoxedRep l))
            (b :: TYPE ('BoxedRep k))
  . a -> b -> Int#
```

This means that it is no longer necessary to use `unsafeCoerce#` when using `reallyUnsafePtrEquality#` on values of different types, or whose types are unlifted, such as `Array#`, `ByteArray#`, `SmallArray#`. In fact, it is no longer possible to use `unsafeCoerce#` in this way, as GHC doesn't support unsaturated levity-polymorphic primops. This means that code such as

```haskell
hetPtrEq :: a -> b -> Bool
hetPtrEq a b = isTrue# (unsafeCoerce# reallyUnsafePtrEquality# a b)
```

will no longer typecheck. To fix this, simply remove the call to `unsafeCoerce#`:

```haskell
hetPtrEq :: a -> b -> Bool
hetPtrEq a b = isTrue# (reallyUnsafePtrEquality# a b)
```

#### `withDict` replaces `magicDict`

`GHC.Prim.magicDict` has been replaced by `GHC.Magic.withDict`. `withDict` takes its arguments in a different order than `magicDict`:

```haskell
withDict :: forall {rr :: RuntimeRep} st dt (r :: TYPE rr). st -> (dt => r) -> r
```

This change was motivated by:

* The old type of `magicDict`, `forall a. a` was difficult to use correctly.
* Moreover, `magicDict` was not expressive enough to handle classes with multiple type arguments (e.g., `Typeable @k (a :: k)`.

Here is an example of how to adapt existing code that uses `magicDict`:

```haskell
newtype SNat (n :: Nat) = SNat Natural

class SNatI (n :: Nat) where
  natSingI :: SNat n

data WrapN a b = WrapN (KnownNat    a => Proxy a -> b)

withSNat :: forall a b.
            (KnownNat a => Proxy a -> b)
         -> SNat a      -> Proxy a -> b
withSNat f x y = magicDict (WrapN f) x y
```

Whereas `magicDict` requires an intermediate data type like `WrapN` to function, `withDict` only requires explicit type applications. As a result, `withSNat` can be defined in terms of `withDict` without the need for `WrapN`:

```haskell
withSNat f x y = withDict @(SNat a) @(KnownNat a) x f y
```

#### Pointer equality primops

The following functions have been moved from `GHC.Prim` to `GHC.Exts`:

- `sameMutableArray#`, `sameSmallMutableArray#`, `sameMutableByteArray#` and `sameMutableArrayArray#`,
- `sameMutVar#`, `sameTVar#` and`sameMVar#`,
- `sameIOPort#`,
- `eqStableName#`.

Code that imported these functions from `GHC.Prim` will need to be modified to import them from `GHC.Exts` instead. Note that this is a backwards-compatible change, as these primops are already re-exported by `GHC.Exts` before GHC 9.4.