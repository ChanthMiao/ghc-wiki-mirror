[[_TOC_]]

# GHC 9.2.x Migration Guide


This guide summarises the changes you may need to make to your code to migrate from GHC 9.0 to GHC 9.2. This guide complements the GHC 9.2.x release notes which should be consulted as well.

---

## Compiler changes

### Kind inference for data family instances is pickier

GHC's kind inference for data/newtype instance declarations is slightly more restrictive than before. The new rule is this: in data/newtype instance declararations (unlike ordinary data/newtype declarations) we do *not* look at the constructor declarations when inferring the kind of the instance header.  The principle is that *the instantiation of the data instance should be apparent from the header alone*.

A consequence of this change is that some programs that compiled on previous versions of GHC will no longer be accepted under 9.2. Here is such an example:

```hs
data family T :: k -> Type
data instance T a = MkT1 a
```

Previous versions of GHC would look at the `MkT1` constructor to infer that `a` has kind `Type` and accept the data instance. GHC 9.2, however, only looks of the header (`data instance T a`) and concludes that `a` has kind `k`. As a result, the `MkT1` constructor is ill kinded, so GHC will throw an `Expected a type, but ‘a’ has kind ‘k’` error.

The workaround is to simply make the kind of `a` explicit:

```hs
data family T :: k -> Type
data instance T (a :: Type) = MkT1 a
```

### Stricter scope-checking for associated type family instances

GHC is now stricter about checking for out-of-scope type variables on the right-hand sides of associated type family instances that are not bound on the left-hand side. As a result, some programs that were accidentally accepted in previous versions of GHC will now be rejected, such as this example:

```hs
class Funct f where
  type Codomain f
instance Funct ('KProxy :: KProxy o) where
  type Codomain 'KProxy = NatTr (Proxy :: o -> Type)
```

Where:

```hs
data Proxy (a :: k) = Proxy
data KProxy (t :: Type) = KProxy
data NatTr (c :: o -> Type)
```
      
GHC will now reject the `o` on the right-hand side of the `Codomain` instance as being out of scope, as it does not meet the requirements for being explicitly bound (as it is not mentioned on the left-hand side) nor
implicitly bound (as it is not mentioned in an _outermost_ kind signature). This program can be repaired in a backwards-compatible way by mentioning `o` on the left-hand side:

```hs
instance Funct ('KProxy :: KProxy o) where
  type Codomain ('KProxy @o) = NatTr (Proxy :: o -> Type)
  -- Alternatively,
  -- type Codomain ('KProxy :: KProxy o) = NatTr (Proxy :: o -> Type)
```

### `UndecidableInstances` no longer implies `FlexibleContexts` in instance declarations

Previous versions of GHC would accept the following program, as the use of `UndecidableInstances` would imply the `FlexibleContexts` extension:

```hs
{-# LANGUAGE UndecidableInstances #-}
module Foo where

data T1 a
data T2 a = MkT2 (T1 a)

instance Eq (T1 a) => Eq (T2 a) where
  MkT2 x == MkT2 y = x == y
```

GHC 9.2 tightens up the specification of `UndecidableInstances`, and one consequence of this is that `UndecidableInstances` no longer implies `FlexibleContexts`. As a result, the program above will no longer typecheck on GHC 9.2 or later:

```
$ ~/Software/ghc-9.1.20210201/bin/ghc Foo.hs
[1 of 1] Compiling Foo              ( Foo.hs, Foo.o )

Foo.hs:7:10: error:
    • Non type-variable argument in the constraint: Eq (T1 a)
      (Use FlexibleContexts to permit this)
    • In the instance declaration for ‘Eq (T2 a)’
  |
7 | instance Eq (T1 a) => Eq (T2 a) where
  |          ^^^^^^^^^^^^^^^^^^^^^^
```

As the error message indicates, this can be fixed in a backwards-compatible way by enabling `FlexibleContexts`.

### Subsumption-related changes to operator sections

GHC 9.2 implements further simplifications related to subsumption when typechecking, in addition to other simplifications made in 9.0. In particular, these changes concern the way that operator sections are typechecked. Some uses of operator sections involving operators with nested `forall`s or contexts in their type signatures will no longer typecheck in GHC 9.2, such as in this example:

```hs
f :: a -> forall b. b -> a
f x _ = x

g :: a -> a
g = (`f` "hello")
```

This will now fail with:

```
Bug.hs:8:6: error:
    • Couldn't match type: forall b. b -> a
                     with: String -> a
      Expected: a -> String -> a
        Actual: a -> forall b. b -> a
    • In the expression: f
      In the expression: `f` "hello"
      In an equation for ‘g’: g = (`f` "hello")
    • Relevant bindings include g :: a -> a (bound at Bug.hs:8:1)
  |
8 | g = (`f` "hello")
  |      ^^^
```

`g` can be made to typecheck once more through eta expansion:

```hs
g :: a -> a
g x = x `f` "hello"
```

### `LiftedRep`/`UnliftedRep` replaced with `BoxedRep`

GHC 9.2 removes the `LiftedRep` and `UnliftedRep` constructors from `GHC.Exts.RuntimeRep` and replaces them with a new `BoxedRep` constructor, as described in [this GHC proposal](https://github.com/ghc-proposals/ghc-proposals/blob/master/proposals/0203-pointer-rep.rst):

```diff
 data RuntimeRep
   = ...
-  | LiftedRep
-  | UnliftedRep
+  | BoxedRep Levity
+
+data Levity = Lifted | Unlifted
```

The `GHC.Types` module now defines the following type synonyms to retain some degree of backwards compatibility with previous versions of GHC:

```hs
type LiftedRep   = 'BoxedRep 'Lifted
type UnliftedRep = 'BoxedRep 'Unlifted
```

### `GHC2021`

When GHC or GHCi is invoked, it will now enable the language extensions included as a part of `GHC2021`. See [here](https://github.com/ghc-proposals/ghc-proposals/blob/f0b86694de131a89a26e12253d922fba186654f2/proposals/0380-ghc2021.rst#id2) for the full list of language extensions. If your project no longer compiles with `GHC2021`, you can disable the appropriate language extensions as needed.

Note that this is unlikely to affect projects with `.cabal` files, as they typically use the `Haskell98` or `Haskell2010` sets of language extensions.

#### PolyKinds

One source of breakages is that `-XPolyKinds` is now enabled by default, as it is included in `GHC2021`. This means that GHC will introduce kind variables instead of defaulting them to `Type`. For example, in

```hs
data D a = MkD
```

without `-XPolyKinds` we infer `D :: Type -> Type`, but with `-XPolyKinds` we infer `D :: forall (k :: Type). k -> Type`. This can cause further problems, e.g. no longer being able to solve an instance for `D a`. You can fix this by disabling `-XPolyKinds` (e.g., use `{-# LANGUAGE NoPolyKinds #-}`), but a more forward-looking solution is to add a kind signature to specify the intended kinds. For example, you can use a [standalone kind signature](https://downloads.haskell.org/ghc/latest/docs/html/users_guide/exts/poly_kinds.html?highlight=standalonekindsignatures#extension-StandaloneKindSignatures):

```hs
type D :: Type -> Type
data D a = MkD
```

or simply directly annotate the type variable with the intended kind:

```hs
data D (a :: Type) = MkD
```

---

## Library changes

### `base-4.16.*`

#### `Nat` is a synonym for `Natural`

The `Nat` kind is now a type synonym for `Natural` (from `Numeric.Natural`). As a consequence, one must enable `TypeSynonymInstances` in order to define instances for `Nat`.

#### `Option` removed from `Data.Semigroup`

The `Option` newtype, which was deprecated in GHC 9.0, is now removed as of 9.2. `Option` was originally introduced to produce an alternative to `Maybe` such that its `Monoid (Option a)` instance only requires `Semigroup a`. However, the `Monoid (Maybe a)` instance was changed in GHC 8.4 to only require a `Semigroup a` constraint, making `Option` redundant. As such, any uses of `Option` can now be replaced with `Maybe`.

#### Changes to the `(<=)` and `(<=?)` type families

A new kind-polymorphic `Compare` type family was added in the `Data.Type.Ord` module, and moreover, the `(<=?)` and `(<=)` type families from `GHC.TypeNats` are now defined in terms of `Compare`. This has two important repercussions:

* GHC can no longer deduce that all natural numbers are greater than or equal to zero. For instance, this program will no longer typecheck:

  ```hs
  test1 :: Proxy (0 <=? x) -> Proxy True
  test1 = id
  ```
* When `(<=)` or `(<=?)` are used very generically, a kind may need to be provided.  For example, this program:

  ```hs
  test2 :: Proxy (x <=? x) -> Proxy True
  test2 = id
  ```

  Will now generate a type error because GHC does not know the kind of `x`. To fix this, one must provide an explicit kind, perhaps by changing the type to:

  ```hs
  test2 :: forall (x :: Nat). Proxy (x <=? x) -> Proxy True
  ```

#### Sized Word/Int use sized Word#/Int# primitives

Before 9.2, `Int{8,16,32}`/`Word{8,16,32}` were all wrappers for `Int#/Word#`, e.g. `data Int8 = I8 Int#`. Now they are wrappers for the appropriate sized primitive types, e.g. `data Int8 = I8 Int8#`. This change was made to properly handle FFI calling conventions on Darwin/ARM64 (see be5d74caab64abf9d986fc7290f62731db7e73e7).

Users of the wrapped unboxed value must use sized primops that come with each primitive type (e.g. `plusInt8# :: Int8# -> Int8# -> Int8#`). In particular each primitive type comes with primops to convert from/to full machine word primitives (e.g. `intToInt8#` and `int8ToInt#`).

### `template-haskell-2.18.*`

Template Haskell now supports the ability to write type applications as arguments to data constructors in patterns. As a result, the `ConP` constructor of `Pat` now accepts a list of `Type`s that represent its visible type arguments, if any are provided:

```diff
 data Pat
   = ...
-  | ConP Name [Pat]
+  | ConP Name [Type] [Pat]
```

For example, the `(Just x)` in `f (Just x) = x` is now represented as `ConP 'Just [] [VarP x]`.